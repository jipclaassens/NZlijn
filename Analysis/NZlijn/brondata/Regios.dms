container Regios
{
	parameter<string> Regio_jaar := '2022'; //analyse op 2022 gedaan.

	unit<uint32> Gebiedsindelingen_jaren := range(uint32, 2000, 2025)
	{
		attribute<uint32> jaar := id(.);
		attribute<string> name := 'Y'+string(jaar);
	}
	container Gebiedsindelingen :=
		for_each_ne(
			Gebiedsindelingen_jaren/name
			, 'Read_Gebiedsindelingen_gpkg_T('+quote(string(Gebiedsindelingen_jaren/jaar))+')'
		);

	Template Read_Gebiedsindelingen_gpkg_T
	{
		parameter<string> jaar;
		//
		container gpkg
		:	StorageName     = "='%SourceDataDir%/CBS/cbsgebiedsindelingen'+jaar+'.gpkg'"
		, 	StorageType     = "gdal.vect"
		,	StorageReadOnly = "True"
		,	SyncMode        = "alltables"
		,	DialogData      = "rdc";
	}
	
	
	unit<uint32>  Buurt := ='Gebiedsindelingen/y'+Regio_jaar+'/gpkg/buurt_niet_gegeneraliseerd' 
	{
		attribute<rdc>       geometry (poly)         := ='Gebiedsindelingen/y'+Regio_jaar+'/gpkg/buurt_niet_gegeneraliseerd/geometry';
		attribute<string>    statcode                := ='Gebiedsindelingen/y'+Regio_jaar+'/gpkg/buurt_niet_gegeneraliseerd/statcode';
		attribute<string>    statnaam                := ='Gebiedsindelingen/y'+Regio_jaar+'/gpkg/buurt_niet_gegeneraliseerd/statnaam';
		attribute<string>    name                    := AsItemName(statnaam);
		attribute<string>    label                   := statnaam;
		
		attribute<Wijk>      Wijk_rel                := point_in_polygon(centroid_or_mid(geometry), wijk/geometry);
		
		attribute<float64>   CI_2021_BU                 := rjoin(id(.), CriminaliteitsIndex/buurt_rel,CriminaliteitsIndex/CI_2021);
		attribute<float64>   CI_2021_WK                 := Wijk/CI_2021[Wijk_rel];
		attribute<uint32>    CI_2021                    := uint32(MakeDefined(CI_2021_BU, CI_2021_WK));
		
		attribute<float64>   CI_2022_BU                 := rjoin(id(.), CriminaliteitsIndex/buurt_rel,CriminaliteitsIndex/CI_2022);
		attribute<float64>   CI_2022_WK                 := Wijk/CI_2022[Wijk_rel];
		attribute<uint32>    CI_2022                    := uint32(MakeDefined(CI_2022_BU, CI_2022_WK));
		
		// container V := for_each_nedv(name, 'value('+string(id(.))+', ..)', void, .);
	}
	
	unit<uint32>  Wijk := ='Gebiedsindelingen/y'+Regio_jaar+'/gpkg/wijk_niet_gegeneraliseerd' 
	{
		attribute<rdc>       geometry (poly)         := ='Gebiedsindelingen/y'+Regio_jaar+'/gpkg/wijk_niet_gegeneraliseerd/geometry';
		attribute<string>    statcode                := ='Gebiedsindelingen/y'+Regio_jaar+'/gpkg/wijk_niet_gegeneraliseerd/statcode';
		attribute<string>    statnaam                := ='Gebiedsindelingen/y'+Regio_jaar+'/gpkg/wijk_niet_gegeneraliseerd/statnaam';
		attribute<string>    name                    := AsItemName(statnaam);
		attribute<string>    label                   := statnaam;
		attribute<Gemeente>  gem_rel := point_in_polygon(centroid_or_mid(geometry), gemeente/geometry);
		
		attribute<float64>   CI_2021                 := rjoin(id(.), CriminaliteitsIndex/wijk_rel,CriminaliteitsIndex/CI_2021);
		attribute<float64>   CI_2022                 := rjoin(id(.), CriminaliteitsIndex/wijk_rel,CriminaliteitsIndex/CI_2022);

		attribute<bool>      IsAmsRegio := gem_rel == gemeente/v/amsterdam 
											|| gem_rel == gemeente/v/amstelveen 
											|| gem_rel == gemeente/v/diemen
											|| gem_rel == gemeente/v/ouder_amstel
											// || gem_rel == gemeente/v/weesp
											|| gem_rel == gemeente/v/aalsmeer
											|| gem_rel == gemeente/v/uithoorn;
											
		unit<uint32> AmsterdamRegio := select_with_attr_by_cond(., IsAmsRegio);
		// container V := for_each_nedv(name, 'value('+string(id(.))+', ..)', void, .);
	}
	
	unit<uint8> VM_regios : nrofrows = 115
	{
		attribute<.> id := id(.);
	}
	
	unit<uint32> CriminaliteitsIndex 
	: StorageName = "%ProjDir%/data/VM_Regios_vertaaltabel_CBSCodes2024_Criminaliteit.xlsx"
	, StorageType = "gdal.vect"
	, StorageReadOnly = "TRUE"
	, SqlString = "SELECT * FROM Data"
	{
		attribute<Buurt> buurt_rel := rlookup(STATCODE, Buurt/Statcode);
		attribute<Wijk>  Wijk_rel  := rlookup(STATCODE, Wijk/Statcode);
	}
	
	
	unit<uint32>  Gemeente := ='Gebiedsindelingen/y'+Regio_jaar+'/gpkg/gemeente_niet_gegeneraliseerd' 
	{
		attribute<rdc>       geometry (poly)         := ='Gebiedsindelingen/y'+Regio_jaar+'/gpkg/gemeente_niet_gegeneraliseerd/geometry';
		attribute<string>    statcode                := ='Gebiedsindelingen/y'+Regio_jaar+'/gpkg/gemeente_niet_gegeneraliseerd/statcode';
		attribute<string>    statnaam                := ='Gebiedsindelingen/y'+Regio_jaar+'/gpkg/gemeente_niet_gegeneraliseerd/statnaam';
		attribute<string>    name                    := AsItemName(statnaam);
		attribute<string>    label                   := statnaam;

		container V := for_each_nedv(name, 'value('+string(id(.))+', ..)', void, .);
	}
	
	unit<uint32> Postcode6 
	:	StorageName     = "%SourceDataDir%/CBS/PC6_y2022.shp"
	,	StorageType     = "gdal.vect"
	,	StorageReadOnly = "True"
	, 	Descr           = "PC6 gebieden in 2022"
	{
		attribute<rdc>              geometry  (polygon) ;
		attribute<string>           label                      	:= PC6;
		attribute<Postcode6>        grid      (NL_grid/domain) 	:= poly2grid(geometry, NL_grid/domain);
		attribute<bool>             IsMRA                       := IsDefined(point_in_polygon(centroid_or_mid(geometry), Brondata/Regios/Wijk/AmsterdamRegio/geometry));
		
		unit<uint32> MRA := select_with_org_rel(IsMRA)
		{
			attribute<geography/rdc>       geometry  (poly) := ../geometry[org_rel];
			attribute<string>              label            := ../label[org_rel];
		}
	}
	
	unit<uint32> Postcode6_areas 
	:	StorageName     = "%SourceDataDir%/CBS/pc6_tm2019_shp/pc6_xy_tm2020_areas.fss"
	,	StorageReadOnly = "True"
	{
		attribute<geography/rdc>       geometry  (poly);
		attribute<geography/rdc>       centroid;
		attribute<string>               label;
		attribute<uint32>               Amsterdam_rel;
		
		attribute<.>                    grid      (NL_grid/domain) 	:= poly2grid(geometry, NL_grid/domain);
		
		unit<uint32> MRA := select_with_org_rel(IsDefined(Amsterdam_rel))
		{
			attribute<geography/rdc>       geometry  (poly) := ../geometry[org_rel];
			attribute<geography/rdc>       centroid         := ../centroid[org_rel];
			attribute<Buurten/src_2021>    buurt_rel        := point_in_polygon(centroid, Buurten/src_2021/geometry);
			attribute<string>              label            := ../label[org_rel], DialogType = "LabelText";
		}
	}	
	unit<uint32> Stadsdelen
	:  StorageName = "%ProjDir%/Data/stadsdelen_2010.shp"
	,  StorageType = "gdal.vect"
	,	StorageReadOnly = "True"
	{
		attribute<geography/rdc> 	geometry (polygon);	
		attribute<stadsdelen>       NL_grid_domain_rel  (NL_grid/domain)         := poly2grid(geometry, NL_grid/domain);
		attribute<string>           Stadsdeeln;
		attribute<uint8>            sdnummer;
		attribute<uint32>           Stadsdeelnummer                                 := sdnummer[uint32];
		attribute<uint32>           point_rel   (sg_points)   := point_in_polygon(sg_points/Geometry, geometry);
	}
 
}
