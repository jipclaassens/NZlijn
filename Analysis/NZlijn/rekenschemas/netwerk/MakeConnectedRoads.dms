////////////////////////////////////////////////////////////////////////////////////////
//                                                                                    //
//                                                                                    //
//                       Configuratie voor project: Open Data Analyse                 //
//                                                                                    //
//                              Object Vision 2011                                    //
//                                                                                    //
////////////////////////////////////////////////////////////////////////////////////////
//                                                                                    //
//                  Configuratiebestand voor opbouw Connected Netwerk                 //
//                                                                                    //
// De niet gesloten links halen we uit de analyse, om te voorkomen dat daar de        //
// begin en eindpunten aan zouden worden gekoppeld, hetgeen tot oneindige afstanden   //
// zou leiden.                                                                        //
//                                                                                    //
// De grote netwerken (> 1km) zijn handmatig doorlopen en hiervoor zijn extra links   //
// toegevoegd.                                                                        //
//                                                                                    //
////////////////////////////////////////////////////////////////////////////////////////

container MakeConnectedRoads
{
	unit <uint32> LinkSet := arc2segm(roads)
	,	DialogData = "segments"
	,	DialogType = "Map"
	{
		attribute<geography/rdc>                point;
		attribute<geography/rdc>                nextpoint;
		// attribute<roadDomain>                    SequenceNr;
		attribute<typeringen/wegtype>            wegtype       := roadDomain/wegtype[Sequence_rel];
		attribute<m>                             lengte        := arc_length(segments, m), DisableStorage = "True";
		attribute<bool>                          isConnected   := all(pointset/isConnected, pointset/ArcID);

		unit<uint32> pointset := Union_Unit(LinkSet, LinkSet)
		{
			attribute<geography/rdc>   point        := Union_Data(pointset, LinkSet/point, LinkSet/nextpoint);
			attribute<bool>             isConnected  := isNull(rlookup(point, NodesNotConnected/point));
			attribute<roadDomain>       roadsnr      := Union_Data(pointset, LinkSet/Sequence_rel, LinkSet/Sequence_rel);
			attribute<LinkSet>          ArcID        := value(ID(pointset) % nrofrows(LinkSet), LinkSet);
			attribute<uint32>           VolgNr       := ID(pointset) / nrofrows(LinkSet);
		}
		attribute<geography/rdc> segments(arc) := points2sequence(pointset/point, pointset/ArcID, pointset/VolgNr);
	}
	unit<uint32> NodeSet := unique(LinkSet/pointset/point)
	,	DialogData = "Values"
	,	DialogType = "Map"
	{
		attribute<geography/rdc>   Values;
		attribute<Linkset>          LinkSetnr := rlookup(Values, LinkSet/point);
		attribute<roadDomain>       roadsnr := LinkSet/Sequence_rel[LinkSetnr];
	}
	attribute<NodeSet> F1(LinkSet)  := rlookup(LinkSet/point,     NodeSet/Values);
	attribute<NodeSet> F2(LinkSet)  := rlookup(LinkSet/nextpoint, NodeSet/Values);

	unit<uint32> networks := connected_parts(F1, F2)
	{
		attribute<uint32>   nr_nodes    := sum(const(1, NodeSet, uint32), partnr);
		attribute<float32>  Lengte      := sum(LinkSet/lengte[NodeSet/LinkSetnr], partnr);
		parameter<uint32>   MaxNrNodex  := max(networks/nr_nodes);
		parameter<networks> main        := rlookup(MaxNrNodex, nr_nodes); // we gaan ervan uit dat het netwerk met de meeste nodes het hoofd netwerk is
	}
	unit<uint32> NodesNotConnected := select_with_org_rel(networks/partnr <> networks/main)
	{
		attribute<geography/rdc> point := NodeSet/Values[org_rel];
	}
	unit<uint32> NodesNotConnectedNetwork_gt5km := select_with_org_rel(networks/partnr <> networks/main && networks/lengte[networks/partnr] > float32(5000))
	{
		attribute<geography/rdc> point := NodeSet/Values[org_rel];
	}
	
	unit<uint32> result := select_with_org_rel(Linkset/isConnected)
	,	DialogType = "Map"
	,	DialogData = "line"
	{
		attribute<geography/rdc>                line (arc) := Linkset/Segments[org_rel];
		attribute<typeringen/wegtype>            wegtype    := Linkset/wegtype[org_rel];
	}
}